/*
 * Copyright (c) 2017. Jahir Fiquitiva
 *
 * Licensed under the CreativeCommons Attribution-ShareAlike
 * 4.0 International License. You may not use this file except in compliance
 * with the License. You may obtain a copy of the License at
 *
 *    http://creativecommons.org/licenses/by-sa/4.0/legalcode
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 * Special thanks to the project contributors and collaborators
 * 	https://github.com/jahirfiquitiva/Blueprint#special-thanks
 */
apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'kotlin-android'
apply from: '../dependencies.gradle'

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    lintOptions {
        abortOnError false
        checkReleaseBuilds false
        disable 'MissingTranslation'
        disable 'GoogleAppIndexingWarning'
    }

    dexOptions {
        preDexLibraries false
        javaMaxHeapSize "2g"
    }

    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode versions.versionCode
        versionName versions.versionName
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            java.srcDirs += 'src/main/kotlin'
            res.srcDirs += [
                    'src/main/res',
                    'src/main/res/drawable',
                    'src/main/res/drawable/app',
                    'src/main/res/drawable/clock_widget',
                    'src/main/res/drawable/launchers_icons',
                    'src/main/res/drawable/shortcuts',
                    'src/main/res/drawable/splash_screens',
                    'src/main/res/drawable/ui',
                    'src/main/res/layout',
                    'src/main/res/layout/activities',
                    'src/main/res/layout/extras',
                    'src/main/res/layout/items',
                    'src/main/res/layout/sections',
                    'src/main/res/layout/widgets',
                    'src/main/res/values',
                    'src/main/res/values/icon_pack',
                    'src/main/res/values/private-values',
                    'src/main/res/values/translations'
            ]
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    // Android Support Libraries
    compile 'com.android.support:support-v13:' + versions.supportLibsTest
    compile 'com.android.support:appcompat-v7:' + versions.supportLibsTest
    compile 'com.android.support:recyclerview-v7:' + versions.supportLibsTest
    compile 'com.android.support:cardview-v7:' + versions.supportLibsTest
    compile 'com.android.support:palette-v7:' + versions.supportLibsTest
    compile 'com.android.support:design:' + versions.supportLibsTest
    compile 'com.android.support:customtabs:' + versions.supportLibsTest
    // Architecture Components
    compile 'android.arch.lifecycle:runtime:' + versions.archComponents
    compile 'android.arch.lifecycle:extensions:' + versions.archComponents
    annotationProcessor 'android.arch.lifecycle:compiler:' + versions.archComponents
    // Kotlin
    compile 'org.jetbrains.kotlin:kotlin-stdlib:' + versions.kotlin
    // Themes Manager
    compile 'com.github.afollestad:aesthetic:' + versions.aesthetic
    // Material Dialogs
    compile 'com.afollestad.material-dialogs:core:' + versions.materialDialogs
    // Navigation
    compile('com.mikepenz:materialdrawer:' + versions.materialDrawer + '@aar') {
        transitive = true
    }
    // OkHttp
    compile 'com.squareup.okhttp3:okhttp:' + versions.okhttp
    // Glide Image Loading
    compile 'com.github.bumptech.glide:glide:' + versions.glide
    // kapt 'com.github.bumptech.glide:compiler:' + versions.glide
    // RecyclerView FastAdapter
    compile('com.mikepenz:fastadapter:' + versions.fastAdapter + '@aar') {
        transitive = true
    }
    // RecyclerView Fast Scroller
    compile 'com.github.plusCubed:recycler-fast-scroll:' + versions.recyclerViewFastScroll
    // Icon Requests Tool
    compile 'com.github.AllanWang:Icon-Request-Tool:' + versions.iconRequestTool
    // Circular ImageView
    compile 'com.github.hdodenhof:CircleImageView:' + versions.circleImageView
    // Better Bottom Bar
    compile 'com.aurelhubert:ahbottomnavigation:' + versions.bottomBar
    // FABs Menu
    compile 'me.jahirfiquitiva:FABsMenu:' + versions.fabsMenu
    // FAB Transformation
    compile 'konifar:fab-transformation:' + versions.fabTransformation
    // compile 'me.jahirfiquitiva:fab-transformation:' + versions.customFabTransformation
    // Chips View
    compile 'com.github.pchmn:MaterialChipsInput:' + versions.chips
    // Subsampling ImageView
    compile 'com.davemorrissey.labs:subsampling-scale-image-view:' + versions.subsImageView
    // In-App Purchases
    compile 'com.anjlab.android.iab.v3:library:' + versions.iaps
    // Muzei API
    //noinspection GradleDynamicVersion
    compile 'com.google.android.apps.muzei:muzei-api:' + versions.muzeiApi
    // License Checker
    compile 'com.github.javiersantos:PiracyChecker:' + versions.checker
}

tasks.whenTaskAdded { task ->
    if (task.name.contains("lint") || task.name.contains("test")) {
        task.enabled = false
    }
}

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

// uncomment to build a jar file in addition to the default aar file
//android.libraryVariants.all { variant ->
//    def name = variant.buildType.name
//    def task = project.tasks.create "jar${name.capitalize()}", Jar
//    task.dependsOn variant.javaCompile
//    task.from variant.javaCompile.destinationDir
//    artifacts.add('archives', task);
//}

//gradle.projectsEvaluated {
//    tasks.withType(JavaCompile) {
//        options.compilerArgs << "-Xbootclasspath/a:" + System.properties.get("java.home") + "/lib/rt.jar"
//    }
//}